<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8">
<title>Environment Mapping</title>
<style>
    body {
        background-color: #FFFFFF;
    }
    label, button {
        margin-left: 30px;
    }
</style>

<!-- shader program for the skybox -->
<script type="x-shader/x-vertex" id="vshaderSKY">
     uniform mat4 projection;
     uniform mat4 modelview;
     attribute vec3 a_coords;
     varying vec3 v_objCoords;
     void main() {
        vec4 eyeCoords = modelview * vec4(a_coords,1.0);
        gl_Position = projection * eyeCoords;
        v_objCoords = a_coords;
     }
</script>
<script type="x-shader/x-fragment" id="fshaderSKY">
     precision mediump float;
     varying vec3 v_objCoords;
     uniform samplerCube skybox;
     void main() {
          gl_FragColor = textureCube(skybox, v_objCoords);
     }
</script>

<!-- shader program for the reflecting object -->
<script type="x-shader/x-vertex" id="vshaderENV">
     uniform mat4 projection;
     uniform mat4 modelview;
     attribute vec3 a_coords;
     attribute vec3 a_normal;
     varying vec3 v_eyeCoords;
     varying vec3 v_normal;
     void main() {
        vec4 eyeCoords = modelview * vec4(a_coords,1.0);
        gl_Position = projection * eyeCoords;
        v_eyeCoords = eyeCoords.xyz;
        v_normal = normalize(a_normal);
     }
</script>
<script type="x-shader/x-fragment" id="fshaderENV">
     precision mediump float;
     varying vec3 vCoords;
     varying vec3 v_normal;
     varying vec3 v_eyeCoords;
     uniform samplerCube skybox;
     uniform mat3 normalMatrix;
     uniform mat3 invViewTrans;
     void main() {
          vec3 N = normalize(normalMatrix * v_normal);
          vec3 V = -v_eyeCoords;
          vec3 R = -reflect(V,N);
          vec3 T = invViewTrans * R; // Transform by inverse of the view transform that was applied to the skybox
          gl_FragColor = textureCube(skybox, T);
     }
</script>

<!-- shader program with lighting -->
<script type="x-shader/x-vertex" id="vshaderLIT">
    attribute vec3 a_coords;
    attribute vec3 a_normal;
    uniform mat4 modelview;
    uniform mat4 projection;
    varying vec3 v_normal;
    varying vec3 v_eyeCoords;

    void main() {
        vec4 coords = vec4(a_coords,1.0);
        vec4 eyeCoords = modelview * coords;
        gl_Position = projection * eyeCoords;
        v_normal = normalize(a_normal);
        v_eyeCoords = eyeCoords.xyz/eyeCoords.w;
    }
</script>

<script type="x-shader/x-fragment" id="fshaderLIT">
    #ifdef GL_FRAGMENT_PRECISION_HIGH
       precision highp float;
    #else
       precision mediump float;
    #endif
    struct MaterialProperties {
        vec3 diffuseColor;
        vec3 specularColor;
        float specularExponent;
    };
    struct LightProperties {
        bool enabled;
        vec4 position;
        vec3 color;
    };
    uniform MaterialProperties material;
    uniform LightProperties lights[3];
    uniform mat3 normalMatrix;
    varying vec3 v_normal;
    varying vec3 v_eyeCoords;
    vec3 lightingEquation( LightProperties light, MaterialProperties material,
                                vec3 eyeCoords, vec3 N, vec3 V ) {
           // N is normal vector, V is direction to viewer.
        vec3 L, R; // Light direction and reflected light direction.
        if ( light.position.w == 0.0 ) {
            L = normalize( light.position.xyz );
        }
        else {
            L = normalize( light.position.xyz/light.position.w - v_eyeCoords );
        }
        if (dot(L,N) <= 0.0) {
            return vec3(0.0);
        }
        vec3 reflection = dot(L,N) * light.color * material.diffuseColor;
        R = -reflect(L,N);
        if (dot(R,V) > 0.0) {
            float factor = pow(dot(R,V),material.specularExponent);
            reflection += factor * material.specularColor * light.color;
        }
        return reflection;
    }
    void main() {
        vec3 normal = normalize( normalMatrix*v_normal );
        vec3 viewDirection = normalize( -v_eyeCoords);  // (Assumes a perspective projection.)
        vec3 color = vec3(0);
        for (int i = 0; i < 3; i++) {
            if (lights[i].enabled) {
                    color += lightingEquation( lights[i], material, v_eyeCoords,
                                                    normal, viewDirection);
            }
        }
        gl_FragColor = vec4(color,1);
    }
</script>

<script src="gl-matrix-min.js"></script>
<script src="basic-object-models-IFS.js"></script>
<script src="teapot-model-IFS.js"></script>
<script src="rotate.js"></script>
<script src="app.js"></script>

</head>

<body onload="init()">
  <h1 id="headline"> Environment Mapping</h1>
  <form>
    <fieldset>
        <legend>Controls:</legend>
            1. Drag with mouse on the picture to rotate the view<br>
            2. Use arrow keys to rotate the object<br>
    </fieldset>
    <fieldset>
        <legend>Options:</legend>
        <label><b>The Reflective Object:</b> <select id="object">
            <option value="100">No Object</option>
            <option value="0">Cube</option>
            <option value="3">Sphere</option>
            <option value="6">Teapot</option>
        </select></label>
        <label><b>Size of The Object:</b> <select id="objectSize">
            <option value="0">Small</option>
            <option value="1">Medium</option>
            <option value="2">Large</option>
        </select></label>
        <label><b>Number of Dynamic Objects:</b><input type="text" id="numObj" size="3" value = "5"></label>
        <label><input type=button value="Enter" onClick="window.location.reload()" style="width:90px; height:25px;font-size: 15px;"></label>
        <label><b>Animating</b><input type="checkbox" id="isAnimate"></label>
        <button id="reset" style="width:90px; height:25px;font-size: 15px;">Reset</button></label>
  </fieldset>
  </form>
  </br>
  <div id="canvas-holder">
      <canvas width=500 height=500 id="mycanvas"></canvas>
  </div>

</body>
</html>
